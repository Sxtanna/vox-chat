plugins {
	id 'idea'
	id 'java'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
}


group 'com.sxtanna.mc'
version '0.9.0'


repositories {
	mavenCentral()
	
	maven {
		url = uri("https://jitpack.io")
	}
	
	maven {
		url = uri("https://papermc.io/repo/repository/maven-public/")
	}
	
	maven {
		url = uri("https://repo.mvdw-software.com/content/groups/public/")
	}
	maven {
		url = uri("https://repo.extendedclip.com/content/repositories/placeholderapi/")
	}
}

dependencies {
	implementation "org.bstats:bstats-bukkit:3.0.0"
	implementation "com.atlassian.commonmark:commonmark:0.17.0"
	
	compileOnly "org.jetbrains:annotations:23.0.0"
	compileOnly "org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT"
	
	compileOnly("me.clip:placeholderapi:2.11.1") {
		transitive = false
	}
	compileOnly("be.maximvdw:MVdWPlaceholderAPI:3.1.1-SNAPSHOT") {
		transitive = false
	}
	
	compileOnly("com.github.LoneDev6:API-ItemsAdder:3.2.3c") {
		transitive = false
	}
	
	testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.2")
	testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
}


tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

java {
	withJavadocJar()
	withSourcesJar()
	
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}

test {
	useJUnitPlatform()
	
	testLogging {
		showStandardStreams = true
		
		events "passed", "skipped", "failed", "standardOut", "standardError"
	}
}

idea {
	module.downloadJavadoc = true
	module.downloadSources = true
}

configurations {
	testImplementation.extendsFrom(compileOnly)
}

shadowJar {
	archiveClassifier.set("")
	
	minimize()
	
	relocate "org.bstats", "com.sxtanna.mc.chat.stat"
	relocate "org.commonmark", "com.sxtanna.mc.chat.libs"
}


processResources {
	from(sourceSets.main.resources.srcDirs) {
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
		
		filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
				pluginName: project.properties["pluginName"],
				pluginMain: project.properties["pluginMain"],
				
				pluginVersion: project.properties["pluginVersion"],
				pluginWebsite: project.properties["pluginWebsite"],
				
				pluginAuthors: project.properties["pluginAuthors"],
		]
	}
}


task copyToServerDir(type: Copy) {
	from shadowJar
	into "${rootProject.rootDir.parent}\\server\\plugins"
}